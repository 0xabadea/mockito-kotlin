apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.nhaarman'
version = rootProject.ext.versionName
def sonatypeUsername = hasProperty('sonatype_username') ? sonatype_username : System.getenv('SONATYPE_USERNAME')
def sonatypePassword = hasProperty('sonatype_password') ? sonatype_password : System.getenv('SONATYPE_PASSWORD')


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(
                        userName: sonatypeUsername,
                        password: sonatypePassword
                )
            }

            if (publishToLocal) repository(url: mavenLocal().url)

            pom.project {
                name 'Mockito-Kotlin'
                packaging 'jar'
                description 'Using Mockito with Kotlin.'
                url 'https://github.com/nhaarman/mockito-kotlin'

                scm {
                    url 'scm:git@github.com:nhaarman/mockito-kotlin.git'
                    connection 'scm:git@github.com:nhaarman/mockito-kotlin.git'
                    developerConnection 'scm:git@github.com:nhaarman/mockito-kotlin.git'
                }

                licenses {
                    license {
                        name 'MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'nhaarman'
                        name 'Niek Haarman'
                    }
                }
            }
        }
    }
}