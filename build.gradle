plugins {
    id "org.shipkit.shipkit-auto-version" version "1.1.1"
    id "org.shipkit.shipkit-changelog" version "1.1.10"
    id "org.shipkit.shipkit-github-release" version "1.1.10"
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}

//TODO: change java packages to "org.mockito.kotlin"
group = 'org.mockito.kotlin'

tasks.named("generateChangelog") {
    previousRevision = project.ext.'shipkit-auto-version.previous-tag'
    githubToken = System.getenv("GITHUB_TOKEN")
    repository = "mockito/mockito-kotlin"
}

tasks.named("githubRelease") {
    def genTask = tasks.named("generateChangelog").get()
    dependsOn genTask
    repository = genTask.repository
    changelog = genTask.outputFile
    githubToken = System.getenv("GITHUB_TOKEN")
    newTagRevision = System.getenv("GITHUB_SHA")
}

nexusPublishing {
    repositories {
        if (System.getenv("NEXUS_TOKEN_PWD")) {
            sonatype {
                username = System.getenv("NEXUS_TOKEN_USER")
                password = System.getenv("NEXUS_TOKEN_PWD")
            }
        }
    }
}
